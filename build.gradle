plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'fr.hashtek.spigot'
version = 'R0.2.0-SNAPSHOT'

def hashrc = 'C:\\Users\\Lysandre\\Documents\\Hashtek\\Plugins\\hashrc'
def libsFolder = 'libs'

repositories {
    mavenCentral()
    maven {
        name = "papermc"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "minecraft-repo"
        url = "https://libraries.minecraft.net/"
    }
}

configurations {
    implementation.canBeResolved = true
}

dependencies {
    compileOnly 'org.github.paperspigot:paperspigot-api:1.8.8-R0.1-SNAPSHOT'
    compileOnly 'com.mojang:authlib:1.5.21'

    implementation fileTree(dir: libsFolder, include: ['*.jar'])
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [
        name: rootProject.name,
        version: version
    ]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    dependencies {
        exclude(dependency('org.github.paperspigot:paperspigot-api:1.8.8-R0.1-SNAPSHOT'))
        exclude(dependency('com.mojang:authlib:1.5.21'))
    }

    archiveClassifier.set('')

    mergeServiceFiles()
    minimize()
}

tasks.register('make', Copy) {
    dependsOn shadowJar

    from ('build/libs')
    into hashrc
    include '*.jar'
}
